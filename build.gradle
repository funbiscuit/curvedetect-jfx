plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'com.gluonhq.client-gradle-plugin' version '0.1.38'
    id 'java'
}

group 'com.funbiscuit.jfx'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml']
}

mainClassName = 'com.funbiscuit.jfx.curvedetect.Main'

dependencies {
    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.18'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.18'

    testImplementation 'junit:junit:4.12'
}

jar {
    archiveFileName.set("${rootProject.getName()}.${archiveExtension.get()}")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE // exclude duplicates
    manifest {
        attributes 'Main-Class': 'com.funbiscuit.jfx.curvedetect.Launcher'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

gluonClient {
    reflectionList = ['javafx.fxml.FXMLLoader',
                      'javafx.collections.FXCollections',
                      'javafx.scene.control.ComboBox',
                      'javafx.scene.control.Slider',
                      'com.funbiscuit.jfx.curvedetect.CanvasPane',
                      'com.funbiscuit.jfx.curvedetect.MainController',
                      'com.funbiscuit.jfx.curvedetect.TickPopupController',
    ]
    bundlesList = [
            // will include only bundle for default locale at build time
            // so need to use a workaround later
            'com.funbiscuit.jfx.curvedetect.strings'
    ]
}
